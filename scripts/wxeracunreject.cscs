Import(strTrim(mpath()) + "CSCS.Math.dll");

args = CommandLineArgs(); 
if(Size(args) > 1){

    companyCode = args[2]; // e.g. Y3
    if(companyCode != ""){  
        CoSet(companyCode);
    }else{
        MessageBox("Nedostaje šifra tvrtke u argumentima.");
        exit;
    }    
    
    userCode = args[3]; // e.g. ANA
    if(userCode != ""){  
    }else{  
        MessageBox("Nedostaje šifra korisnika u argumentima.");
        exit;
    }    
    
    invNum = args[4]; // e.g. 90100002 // aurasoft invoice number
    if(invNum != 0){  
    }else{  
        MessageBox("Nedostaje broj računa u argumentima.");
        exit;
    }

    newRejectStatus = args[5]; // new status code -> APPROVE/REJECT
    if (newRejectStatus != ""){     
        newRejectStatus = newRejectStatus.Upper();
        if(newRejectStatus != "APPROVED" && newRejectStatus != "REJECTED"){
            MessageBox("Novi status odbijanja/prihvaćanja nije ispravan. Dozvoljene vrijednosti su: APPROVED ili REJECTED.");
            exit;
        }
    }
    else{
        MessageBox("Nedostaje novi status odbijanja/prihvaćanja u argumentima.");
        exit;
    }

    rejectReason = args[6]; // Rejection reason -> VAT_REASON/NOT_VAT_REASON/OTHER_REASON
    if (rejectReason != ""){
        rejectReason = rejectReason.Upper();
        if(rejectReason != "VAT_REASON" && rejectReason != "NOT_VAT_REASON" && rejectReason != "OTHER_REASON"){
            MessageBox("Razlog odbijanja nije ispravan. Dozvoljene vrijednosti su: VAT_REASON, NOT_VAT_REASON ili OTHER_REASON.");
            exit;
        }
    }
    else{
        rejectReason = "OTHER_REASON"; //!!! default value is?
        exit;
    }

    rejectNote = args[7]; // Rejection note
    if (rejectNote == ""){
        rejectNote = ""; // Optional, empty by default
    }

}else{
    MessageBox("Nedostaju argumenti komandne linije.\nKorištenje: wxeracunreject.cscs <companyCode> <userCode> <invNum> <newRejectStatus> <rejectReason> <rejectNote>");
    exit;
}   

// NKSYCCYR
sqlQueryString = "select SY_CC_USER, SY_CC_DBASE from " + strtrim(CommonDBGet()) + ".dbo.NKSYCCYR WHERE SY_CC_CODE = '" + companyCode + "'";
try
{
    sqlResult = sqlQuery(sqlQueryString);
}
catch(exc)
{
    MessageBox("NKSYCCYR reading error: " + exc);
    exit;
}
if (size(sqlResult) > 1)
{
    SY_CC_USER = sqlResult[1][0].trim();
    databaseName = sqlResult[1][1].trim();
}    

// KPSYMSTR
sqlQueryString = " SELECT TOP 1 KPSY_REZERVA, KPSY_COMP_NAME, KPSY_COMP_JMBFI, KPSY_REZERVAX
                    FROM " + databaseName + ".dbo.KPSYMSTR";
try
{
    sqlResult = sqlQuery(sqlQueryString);
}
catch(exc)
{
    MessageBox("KPSYMSTR reading error: " + exc);
    exit;
}
if (size(sqlResult) > 1)
{
    KPSY_REZERVA = sqlResult[1][0].trim();
    KPSY_COMP_NAME = sqlResult[1][1].trim();
    KPSY_COMP_JMBFI = sqlResult[1][2].trim();
    KPSY_REZERVAX = sqlResult[1][3].trim(); // Supplier GLN
    supOIB = Substring(KPSY_REZERVA, 0, 11);
} 

// NKSYSYCO
sqlQueryString = " SELECT NKSYS_FIELD, NKSYS_VALUE 
    FROM " + databaseName + ".dbo.NKSYSYCO 
    WHERE NKSYS_MODUL = 'WKSY'
    AND NKSYS_GRUP = 'ERACUN'";
try
{
    sqlResult = sqlQuery(sqlQueryString);
}
catch(exc)
{
    MessageBox("NKSYSYCO reading error: " + exc);
    exit;
}
if (size(sqlResult) > 1)
{
    for(i = 1; i < Size(sqlResult); i++){
        if(sqlResult[i][0].trim() == "FILESPATH"){
            FILESPATH = sqlResult[i][1].trim();
        }
        elif(sqlResult[i][0].trim() == "PROVIDER")
        {
            PROVIDER = sqlResult[i][1].trim().Upper();
        }elif(sqlResult[i][0].trim() == "CERTPATH"){
            CERTPATH = sqlResult[i][1].trim();
        }
    }
} 

// Load invoice header to get customer and provider info
sql_hdr = "SELECT
            NKPR_GL_NUM, NKPR_GL_OZNAKA, 
            CONVERT(VARCHAR(10), NKPR_GL_INVDTE, 23) as NKPR_GL_INVDTE,
            NKPR_GL_CUSNME, NKPR_GL_JMBG, NKPR_GL_CUSCOD,
            NKSC_SOP, NKSC_POSREDNIK, NKSC_REZ10
            FROM " + databaseName + ".dbo.NKPRINV 
            LEFT JOIN " + databaseName + ".dbo.NKSCPART ON NKPR_GL_CUSCOD = NKSC_PARTCODE
            WHERE NKPR_GL_NUM = @p1;";
sqlParams = {};
sqlParams.Add({"@p1", invNum});

hdrRes = sqlQuery(sql_hdr, sqlParams);
if (Size(hdrRes) < 2) {
    MessageBox("Ne postoji račun " + invNum + "!");
    exit;
}

NKPR_GL_NUM = hdrRes[1][0];
NKPR_GL_OZNAKA = hdrRes[1][1].trim();
NKPR_GL_INVDTE = hdrRes[1][2].trim();
NKPR_GL_CUSNME = hdrRes[1][3].trim();
NKPR_GL_JMBG = hdrRes[1][4].trim();
NKPR_GL_CUSCOD = hdrRes[1][5].trim();
NKSC_SOP = hdrRes[1][6].trim(); // Customer GLN
NKSC_POSREDNIK = hdrRes[1][7].trim().Upper(); // Intermediary GLN
NKSC_REZ10 = hdrRes[1][8].trim().Upper(); // P - send to server / F = save file to disk

// if(NKSC_POSREDNIK != ""){    
//     PROVIDER = NKSC_POSREDNIK;
// }

// Load provider configuration
sql_erac = "SELECT
            ERAC_REZ1, ERAC_SERVER, ERAC_SERVERT, ERAC_USER, ERAC_PASS, ERAC_USERT, ERAC_PASST, ERAC_DESC, ERAC_CODE 
            FROM " + databaseName + ".dbo.NKSYERAC WHERE ERAC_CODE = @p1;";
sqlParams = {};
sqlParams.Add({"@p1", PROVIDER});

eracRes = sqlQuery(sql_erac, sqlParams);
if (Size(eracRes) < 2) {
    MessageBox("Nema podataka o posredniku " + PROVIDER + "!");
    exit;
}

ERAC_REZ1 = eracRes[1][0].trim();
ERAC_SERVER = eracRes[1][1].trim();
ERAC_SERVERT = eracRes[1][2].trim();
ERAC_USER = eracRes[1][3].trim();
ERAC_PASS = eracRes[1][4].trim();
ERAC_USERT = eracRes[1][5].trim(); // cer certificate
ERAC_PASST = eracRes[1][6].trim(); // p12 certificate
ERAC_DESC = eracRes[1][7].trim();
ERAC_CODE = eracRes[1][8].trim();

// Send rejection
if(!SendRejectERacun()){
    MessageBox("Greška pri promjeni statusa e-računa " + invNum + "!");    
    exit;
}

exit;

//-----------------------------------------------------------
// SEND REJECTION FUNCTION
//-----------------------------------------------------------

function SendRejectERacun() {
    if(PROVIDER == "MER" || PROVIDER == "MERDEMO"){
        // M.E.R. rejection
        return SendRejectMER();
    }
    elif(PROVIDER == "EPOS" || PROVIDER == "EPOSDEMO")
    {
        // ePoslovanje.hr rejection
        return SendRejectEPOS();
    }
    elif(PROVIDER == "FINA" || PROVIDER == "FINADEMO")
    {
        // FINA rejection
        return SendRejectFINA();
    }
    elif(PROVIDER == "EDITEL" || PROVIDER == "EDITELDEMO"){
        MessageBox("Odbijanje eRačuna putem posrednika " + PROVIDER + " nije predviđeno ovim programom.");
        return false;
    }
    else
    {
        MessageBox("Nepoznat tip posrednika " + ERAC_CODE + " za odbijanje eRačuna.");
        return false;
    }    
}

//-----------------------------------------------------------
// MER REJECTION
//-----------------------------------------------------------

function SendRejectMER() {
    try {
        // M.E.R. API v2 rejection endpoint
        // Need to find the electronic ID first, then send rejection
        
        jsonLoad = '{"Username": "' + ERAC_USER + '", "Password": "' + ERAC_PASS + '", "CompanyId": "' + supOIB + '"' + 
                   ', "SoftwareId": "' + ERAC_DESC + '", "InvoiceNumber": "' + invNum + '"' + 
                   ', "RejectCode": "' + rejectCode + '", "RejectNote": "' + rejectNote + '"}';

        webRequestResult = WebRequest("POST", ERAC_SERVER + "apis/v2/reject", jsonLoad, invNum,
                    "MERRejectSuccess", "MERRejectFailure", "application/json", null, 1000 * 20, false);

        return true;
    } catch(ex) {
        MessageBox("Greška pri slanju odbijanja na M.E.R.: " + ex);
        LogNKSYELOG("B", "MER Reject error: " + ex);
        return false;
    }
}

function MERRejectSuccess(_invID, responseCode, res) {
    try{
        LogResponse(responseCode, res);

        resObject = DeserializeJson(res);    
        if(Contains(resObject, "StatusId") && resObject["StatusId"] == 20){
            MessageBox("Odbijanje dokumenta uspješno poslano.");
            LogNKSYELOG("R", "Rejection sent: Code=" + rejectCode + ", Note=" + rejectNote);
        }
        else{
            MessageBox("GREŠKA pri slanju odbijanja dokumenta.");
            LogNKSYELOG("B", "MER Reject failed: Unknown error");
        }
    }
    catch(ex){
        MessageBox("MERRejectSuccess Exception - Greška pri parsiranju odgovora poslužitelja:\n" + ex);
    }
}

function MERRejectFailure(_invID, responseCode, res) {
    try{
        LogResponse(responseCode, res);

        MessageBox("GREŠKA pri slanju odbijanja dokumenta na M.E.R. server.\nStatus code: " + responseCode);
        
        if(res != ""){
            resObject = DeserializeJson(res);
            if(Contains(resObject, "ErrorMessage")){
                MessageBox("Poruka greške: " + resObject["ErrorMessage"]);
                LogNKSYELOG("B", "MER Reject failure: " + resObject["ErrorMessage"]);
            }
        }
    }
    catch(ex){
        MessageBox("MERRejectFailure Exception:\n" + ex);
        LogNKSYELOG("B", "MER Reject failure exception: " + ex);
    }
}

//-----------------------------------------------------------
// EPOS REJECTION
//-----------------------------------------------------------

function SendRejectEPOS() {
    try {
        // Determine endpoint
        endpoint = ERAC_SERVER;
        if(PROVIDER == "EPOSDEMO"){
            endpoint = ERAC_SERVERT;
        }

        // ePoslovanje API v2 rejection endpoint
        jsonPayload = {
            "invoiceNumber": invNum,
            "rejectCode": rejectCode,
            "rejectNote": rejectNote,
            "softwareId": ERAC_DESC
        };

        jsonLoad = SerializeJson(jsonPayload);

        // Prepare headers with API key authentication
        headers = {};
        headers["Authorization"] = ERAC_USER; // API Key
        headers["Content-Type"] = "application/json";

        webRequestResult = WebRequest(
            "POST",
            endpoint + "/api/v2/document/reject",
            jsonLoad,
            invNum,
            "EPOSRejectSuccess",
            "EPOSRejectFailure",
            "application/json",
            headers,
            1000 * 30,
            false
        );
        
        return true;
    } catch (ex) {
        MessageBox("Greška pri slanju odbijanja na ePoslovanje: " + ex);
        LogNKSYELOG("B", "EPOS Reject error: " + ex);
        return false;
    }
}

function EPOSRejectSuccess(_invID, responseCode, res) {
    try {
        LogResponse(responseCode, res);
        
        resObject = DeserializeJson(res);
        if(Contains(resObject, "success") && resObject["success"]){
            MessageBox("Odbijanje dokumenta uspješno poslano na ePoslovanje.");
            LogNKSYELOG("R", "EPOS Rejection sent: Code=" + rejectCode + ", Note=" + rejectNote);
        }
        else{
            MessageBox("GREŠKA pri slanju odbijanja na ePoslovanje.");
            LogNKSYELOG("B", "EPOS Reject: Unknown error");
        }
    }
    catch(ex){
        MessageBox("EPOSRejectSuccess Exception:\n" + ex);
    }
}

function EPOSRejectFailure(_invID, responseCode, res) {
    try {
        LogResponse(responseCode, res);
        
        MessageBox("GREŠKA pri slanju odbijanja na ePoslovanje server.\nStatus code: " + responseCode);
        
        if(res != ""){
            resObject = DeserializeJson(res);
            if(Contains(resObject, "message")){
                MessageBox("Poruka greške: " + resObject["message"]);
                LogNKSYELOG("B", "EPOS Reject failure: " + resObject["message"]);
            }
        }
    }
    catch(ex){
        MessageBox("EPOSRejectFailure Exception:\n" + ex);
        LogNKSYELOG("B", "EPOS Reject failure exception: " + ex);
    }
}

function GetInvoiceId(aurasoftInvoiceNumber) {
    // sqlString = "SELECT TOP 1 SY_ELOG_REZ FROM " + databaseName + ".dbo.NKSYELOG 
    //             WHERE 
    //             SY_ELOG_NUM = @p1 
    //             AND SY_ELOG_TYPE = 'UR'
    //             AND SY_ELOG_STATUS = 'P' 
    //             ORDER BY SY_ELOG_DATE DESC, SY_ELOG_TIME DESC;"; //!!!
    sqlString = "SELECT TOP 1 NKPR_GL_POSRED FROM " + databaseName + ".dbo.NKPRINV 
                WHERE
                NKPR_GL_NUM = @p1 "; //!!!
    sqlParams = {};
    sqlParams.Add({"@p1", aurasoftInvoiceNumber});
    sqlResult = sqlQuery(sqlString, sqlParams);
    if (Size(sqlResult) > 1) {
        return sqlResult[1][0].trim();
    } else {
        MessageBox("Ne mogu pronaći invoiceId za račun " + aurasoftInvoiceNumber + " u NKSYELOG.");
        exit;
    }
}

//-----------------------------------------------------------
// FINA REJECTION
//-----------------------------------------------------------

function SendRejectFINA() {
    try {
        invoiceId = GetInvoiceId(invNum);

        // FINA uses SOAP/WSDL for rejection
        // The rejection is sent using FINA_B2B_INCOMING_INVOICE_ACTION or similar method
        
        MSG("Slanje odbijanja računa na FINA-u putem WSDL-a...");
        
        // Prepare rejection parameters
        ack = FINA_CHANGE_B2B_INCOMING_INVOICE_STATUS(
            ERAC_SERVER + "B2BFinaInvoiceWebService/services/B2BFinaInvoiceWebService",
            ERAC_DESC, // Software ID
            ERAC_USERT, // Service certificate
            ERAC_PASST, // P12 certificate (client)
            ERAC_PASS, // Password for p12 certificate

            UUID(), // messageId
            "9934:" + supOIB, // buyerId (receiver of original invoice, now rejecting)
            invoiceId, // invoiceId (FINA-in)
            newRejectStatus, // new StatusCode
            rejectReason, // CodeReason
            rejectNote // Note
        );
        
        MSG("ack:\n" + ack);

        responseCode = "FINA_CHANGE_INCOIMING_INVOICE_STATUS";
        LogResponse(responseCode, ack);
 
        if(ack["AckStatusCode"] == 10){
            
            return true;

            // treba negdi upisat da je rejected/approved ??? //!!!

            // if(ack["Correct"]){
            //     MSG("Odbijanje dokumenta uspješno poslano na FINA-u.");
            //     LogNKSYELOG("R", "FINA Rejection sent: Code=" + rejectCode + ", Note=" + rejectNote);
            //     return true;
            // }else{
            //     MSG("ack[\"ErrorCode\"] = " + ack["ErrorCode"]);
            //     MSG("ack[\"ErrorMessage\"] = " + ack["ErrorMessage"]);
            //     LogNKSYELOG("B", "FINA Reject error: " + ack["ErrorMessage"]);
            //     return false;
            // }
        }else{
            // MessageBox("AckStatusText = " + ack["AckStatusText"]);
            // LogNKSYELOG("B", "FINA Reject failed: " + ack["AckStatusText"]);
            
            return false;
        }
    }catch(ex){
        MessageBox("Nepoznata greška pri promjeni statusa računa na FINA-i:\n" + ex);
        LogException(ex);

        // LogNKSYELOG("B", "FINA Reject exception: " + ex);
        return false;
    }
}

//-----------------------------------------------------------
// HELPER FUNCTIONS
//-----------------------------------------------------------

function LogResponse(responseCode, response) {
    if(!DIR_EXISTS(FILESPATH + "ErecunReject\\" + "responses\\")){
        MAKE_DIR(FILESPATH + "ErecunReject\\" + "responses\\");
    }

    // logPath = FILESPATH + "ErecunReject\\" + "responses\\" + invNum + ".response";
    // timestamp = Now("yyyy-MM-dd HH:mm:ss");
    // logLine = timestamp + " | Invoice: " + invNum + " | Code: " + responseCode + " | Response: " + response + "\r\n";
    
    // // Append to log file
    // currentLog = "";
    // if(FileExists(logPath)){
    //     currentLog = ReadFile(logPath);
    // }
    // SaveFile(currentLog + logLine, logPath);

    logFilePath = FILESPATH + "ErecunReject\\" + "responses\\" + invNum + ".response"; 

    appendText = Now("yyyy-MM-dd HH:mm:ss") + " -----------------------------------\n\n" + "StatusCode = " + responseCode + "\n\n" + "Response:\n" + response + "\n\n";

    try {
        SaveFile(appendText, logFilePath, "a");        
    } catch (ex) {
        MessageBox("CRITICAL: Cannot write to log file: " + logFilePath + "\nError: " + ex);
    }
}

function LogException(exText) 
{
    if(!DIR_EXISTS(FILESPATH + "ErecunReject\\" + "responses\\"))
    {
        MAKE_DIR(FILESPATH + "ErecunReject\\" + "responses\\");
    }
    logFilePath = FILESPATH + "ErecunReject\\" + "responses\\" + invNum + ".response";
        
    appendText = Now("yyyy-MM-dd HH:mm:ss") + " -----------------------------------\n\n" + "Exception = " + exText + "\n\n";
    
    try {
        SaveFile(appendText, logFilePath, "a");        
    } catch (ex) {
        MessageBox("CRITICAL: Cannot write to log file: " + logFilePath + "\nError: " + ex);
    }
}

function LogNKSYELOG(elogStatusCode, description = ""){
    sqlQueryString = "INSERT INTO " + databaseName + ".dbo.NKSYELOG 
    (sy_elog_num, sy_elog_date, sy_elog_status, sy_elog_person, sy_elog_estring, sy_elog_type, sy_elog_time, sy_elog_desc, sy_elog_rez3)
    VALUES (@p1, @p2, @p3, @p4, @p5, @p6, @p7, @p8, @p9);";

    sqlParams = {};
    sqlParams.Add({"@p1", NKPR_GL_NUM});
    sqlParams.Add({"@p2", NKPR_GL_INVDTE});
    sqlParams.Add({"@p3", elogStatusCode.Upper()});
    sqlParams.Add({"@p4", userCode});
    sqlParams.Add({"@p5", "" });
    sqlParams.Add({"@p6", "RJ"}); // RJ = Reject
    sqlParams.Add({"@p7", Now("HH:mm:ss")});
    sqlParams.Add({"@p8", description});
    sqlParams.Add({"@p9", Now("yyyy-MM-dd")});
    
    try {
        sqlQuery(sqlQueryString, sqlParams);
    } catch(ex) {
        MSG("LogNKSYELOG error: " + ex);
    }
    return;
}

// function LogException(ex) { //NI DOBRO OVAKO
//     try {
//         if(!DIR_EXISTS(FILESPATH + "ErecunReject\\" + "responses\\")){
//             MAKE_DIR(FILESPATH + "ErecunReject\\" + "responses\\");
//         }

//         logPath = FILESPATH + "ErecunReject\\" + "responses\\" + invNum + ".response";
//         timestamp = Now("yyyy-MM-dd HH:mm:ss");
//         logLine = timestamp + " | Invoice: " + invNum + " | Exception: " + ex + "\r\n";
        
//         currentLog = "";
//         if(FileExists(logPath)){
//             currentLog = ReadFile(logPath);
//         }
//         SaveFile(currentLog + logLine, logPath);
//     } catch (logEx) {
//         MSG("Exception logging error: " + logEx);
//     }
// }


/*

// ===============================================================
// WXERACUNREJECT.CSCS - E-INVOICE REJECTION SCRIPT
// ===============================================================
//
// USAGE:
// WXEracunReject.cscs <companyCode> <userCode> <invNum> <rejectCode> <rejectNote>
//
// PARAMETERS:
// - companyCode: Company code (e.g., Y3)
// - userCode: User code (e.g., ANA)
// - invNum: Invoice number to reject (e.g., 90100002)
// - rejectCode: Rejection code (provider-specific)
// - rejectNote: Rejection reason/note (optional, can be empty string "")
//
// EXAMPLE:
// WXEracunReject.cscs Y3 ANA 90100002 "INVALID_TAX" "PDV nije ispravno obračunat"
//
// REJECTION CODES (typical values, check with provider):
// - INVALID_TAX: Invalid tax calculation
// - WRONG_AMOUNT: Incorrect amount
// - DUPLICATE: Duplicate invoice
// - NOT_ORDERED: Goods/services not ordered
// - WRONG_RECIPIENT: Wrong recipient
// - OTHER: Other reason (specify in note)
//
// SUPPORTED PROVIDERS:
// - MER/MERDEMO: M.E.R. (Međunarodni elektronski račun)
// - FINA/FINADEMO: FINA e-račun B2B
// - EPOS/EPOSDEMO: ePoslovanje.hr
// - EDITEL/EDITELDEMO: Not supported yet
//
// LOGGING:
// - Status R: Rejection sent successfully
// - Status B: Rejection failed (error)
// - Type RJ: Reject operation
//
// ===============================================================

*/
