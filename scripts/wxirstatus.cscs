Import(strTrim(mpath()) + "CSCS.Math.dll");

CreateWindow(strTrim(tpath()) + "wxirstatus.xaml", strTrim(ipath()) + "icons\\" + "wxirstatus.ico");
// CreateWindow(strTrim(tpath()) + "wxirstatus.xaml");

DEFINE Rbr type i;
DEFINE cntr1 type i;

DEFINE arrayRbr type i size 100 array 10000;
DEFINE arrayKupac type a size 500 array 10000;
DEFINE arrayOibKupca type a size 11 array 10000;
DEFINE arrayStatusRacuna type a size 100 array 10000;
DEFINE arrayDatumIzdavanja type a size 10 array 10000;
DEFINE arrayDatumKnjizenja type a size 10 array 10000;
DEFINE arrayBrojRacuna type a size 50 array 10000;

DEFINE arrayBrojRacunaFina type a size 50 array 10000;
DEFINE arrayBrojRacunaMer type a size 50 array 10000;

DEFINE arrayIdRacunaPosrednika type a size 50 array 10000;
DEFINE arrayStatusFiskalizacije type a size 50 array 10000;
DEFINE arrayReportPaymentStatus type a size 50 array 10000;

DEFINE FilterDateFrom type d size 10;
DEFINE FilterDateTo type d size 10;

// FilterDateFrom = Now("dd/MM/yyyy");
// FilterDateTo = Now("dd/MM/yyyy");

dtNow = DateTime();

doubleToday = Math.Trunc(DateTimeToDouble(dtNow));

FilterDateFrom = DoubleToDateTime(doubleToday - 30).ToString("yyyy-MM-dd");
FilterDateTo = DoubleToDateTime(doubleToday + 7).ToString("yyyy-MM-dd");

// savedXmlFilesList = {};

function wxirstatus_onDisplay(){
    cursor("wait");
    SetWidgetOptions("btnPrikazi", "IsEnabled", "false");
    SetWidgetOptions("StatusLabel", "Content", "Preuzimam listu izlaznih e-računa.");
    SetWidgetOptions("StatusLabel2", "Content", "Molim pričekajte...");

    try{
        init();
        SetWindowOptions("wxirstatusWindow", "title", "Pregled statusa izlaznih e-računa(" + PROVIDER + ") za " + KPSY_COMP_NAME);
        // ✅ Enable sorting and resizing programmatically
        SetWidgetOptions("datagridListaRacuna", "CanUserSortColumns", "true");
        SetWidgetOptions("datagridListaRacuna", "CanUserResizeColumns", "true");
    }catch(ex){
        MessageBox("Greška pri inicijalizaciji:\n" + ex);
        // ??? LOG? ne?
        exit;
    }
    
    cursor("dflt");
    SetWidgetOptions("btnPrikazi", "IsEnabled", "true");
    SetWidgetOptions("StatusLabel", "Content", "");
    SetWidgetOptions("StatusLabel2", "Content", "");
}

function init(){
    args = CommandLineArgs(); 
    if(Size(args) > 2){
        companyCode = args[2]; // e.g. Y3
        CoSet(companyCode);

        if(Size(args) > 3){
            userCode = args[3]; // e.g. ANA

        }else{
            MessageBox("Nedostaje user code u argumentima.");
            exit;
        }
    }else{
        companyCode = Substring(CoGet(), 1, 2);
    }


    // NKSYCCYR
    sqlQueryString = "select SY_CC_USER, SY_CC_DBASE from " + strtrim(CommonDBGet()) + ".dbo.NKSYCCYR WHERE SY_CC_CODE = '" + companyCode + "'";
    try
    {
        sqlResult = sqlQuery(sqlQueryString);
    }
    catch(ex)
    {
        MessageBox("Greška čitanja NKSYCCYR:\n" + ex);
        // ??? LOG? ne?
        exit;
    }

    if (size(sqlResult) > 1)
    {
        // imeFirme = sqlResult[1][0].trim(); // KAMEND
        // ovagod_h = sqlResult[1][1].trim(); // 2022
        SY_CC_USER = sqlResult[1][0].trim(); // KAMEND
        databaseName = sqlResult[1][1].trim(); // ime baze
    }    


    // NKSYSYCO Configuration
    sqlQueryString = " SELECT NKSYS_FIELD, NKSYS_VALUE 
        FROM " + databaseName + ".dbo.NKSYSYCO 
        WHERE NKSYS_MODUL = 'WKSY'
        AND NKSYS_GRUP = 'ERACUN'";
    try
    {
        sqlResult = sqlQuery(sqlQueryString);
    }
    catch(ex)
    {
        MessageBox("Greška čitanja NKSYSYCO:\n" + ex);
        // ??? LOG? ne?
        exit;
    }

    if (size(sqlResult) > 1)
    {
        for(i = 1; i < Size(sqlResult); i++){
            if(sqlResult[i][0].trim() == "FILESPATH"){
                FILESPATH = sqlResult[i][1].trim();
            }
            elif(sqlResult[i][0].trim() == "PROVIDER")
            {
                PROVIDER = sqlResult[i][1].trim();
            }
            elif(sqlResult[i][0].trim() == "SCHEMASPATH")
            {
                SCHEMASPATH = sqlResult[i][1].trim();
            }elif(sqlResult[i][0].trim() == "CERTPATH"){
                CERTPATH = sqlResult[i][1].trim();
            }
        }
    }

    logFolder = FILESPATH + "IRStatus\\" + "log\\";
    if (!Exists(logFolder)) {
        MkDir(logFolder);
    }


    // KPSYMSTR
    sqlQueryString = " SELECT TOP 1 KPSY_REZERVA, KPSY_COMP_NAME, KPSY_COMP_ADD1, KPSY_GL_RETEARN, KPSY_COMP_CSZ, KPSY_COMP_ZIP,
                    KPSY_COMP_JMBFI, KPSY_COMP_PHONE, KPSY_COMP_EMAIL, KPSY_PO_FREIGHT, KPSY_REZERVAX,KPSY_FISCAL_YR
                        FROM " + databaseName + ".dbo.KPSYMSTR";
    try
    {
        sqlResult = sqlQuery(sqlQueryString);
    }
    catch(ex)
    {
        MessageBox("Greška čitanja KPSYMSTR:\n" + ex);
        // !!! LOG ???
        exit;
    }

    if (size(sqlResult) > 1)
    {
        // proslaGodina = (int(ovagod_h) - 1);
        // nazivFirme = sqlResult[1][0].trim();
        // kpsy_po_freight= sqlResult[1][1].trim();
        // fiscal_yr = sqlResult[1][2];
        // local = sqlResult[1][3];

        KPSY_REZERVA = sqlResult[1][0].trim();
        KPSY_COMP_NAME = sqlResult[1][1].trim();
        KPSY_COMP_ADD1 = sqlResult[1][2].trim();
        KPSY_GL_RETEARN = sqlResult[1][3].trim();
        KPSY_COMP_CSZ = sqlResult[1][4].trim();
        KPSY_COMP_ZIP = sqlResult[1][5];
        KPSY_COMP_JMBFI = sqlResult[1][6].trim();
        KPSY_COMP_PHONE = sqlResult[1][7].trim();
        KPSY_COMP_EMAIL = sqlResult[1][8].trim();
        KPSY_PO_FREIGHT = substring(sqlResult[1][9], 0, 2).trim();
        KPSY_REZERVAX = sqlResult[1][10].trim(); // Supplier GLN
        KPSY_FISCAL_YR = sqlResult[1][11].trim();
    } 
}

//-----------------------------------------------------------

function getList(){
    try {
        sql_erac = "SELECT
                ERAC_REZ1, ERAC_SERVER, ERAC_SERVERT, ERAC_USER, ERAC_PASS, ERAC_USERT, ERAC_PASST, ERAC_DESC 
                FROM " + databaseName + ".dbo.NKSYERAC WHERE ERAC_CODE = @p1;";
        sqlParams = {};
        // if(NKSC_POSREDNIK != ""){
        //     sqlParams.Add({"@p1", NKSC_POSREDNIK});
        //     PROVIDER = NKSC_POSREDNIK;
        // }else{
            sqlParams.Add({"@p1", PROVIDER});
        // }
        
        eracRes = sqlQuery(sql_erac, sqlParams);
        if (Size(eracRes) < 2) {
            MessageBox("Nema podataka o posredniku " + PROVIDER + "!");
            // !!! LOG !!!
            exit;
        }

        ERAC_REZ1 = eracRes[1][0].trim();
        ERAC_SERVER = eracRes[1][1].trim();
        ERAC_SERVERT = eracRes[1][2].trim();
        ERAC_USER = eracRes[1][3].trim();
        ERAC_PASS = eracRes[1][4].trim();
        ERAC_USERT = eracRes[1][5].trim(); // cer certificate
        ERAC_PASST = eracRes[1][6].trim(); // p12 certificate
        ERAC_DESC = eracRes[1][7].trim();
        
        companyOIB = substring(KPSY_REZERVA, 0, 11).trim();

        if (PROVIDER == "MER" || PROVIDER == "MERDEMO") {
            GetListMER();
        } elif (PROVIDER == "FINA" || PROVIDER == "FINADEMO") {
            GetListFINA_B2B();
            //GetListFINA_B2G();
        } else {
            MessageBox("Nepoznat provider: " + PROVIDER + ". Podržani su: MER, FINA");
            exit;
        }
    } catch (ex) {
        MessageBox("Greška u preuzimanju sa provider-a " + PROVIDER + ": " + ex);
        // !!! LOG !!!
        exit;
    }
}

function GetListFINA_B2B(){
    // GET INCOMING INVOICE LIST
    try{
        // MSG("Dohvaćam listu ULAZNIH računa preko FINA-e putem WSDL-a!");
        ack = FINA_GET_B2B_OUTGOING_INVOICE_LIST(
            ERAC_SERVER + "B2BFinaInvoiceWebService/services/B2BFinaInvoiceWebService",
            ERAC_DESC, // "e-invoice" / NE "e-racun-winx" !
            CERTPATH + ERAC_USERT, // servisni certifikat
            CERTPATH + ERAC_PASST, // p12 certifikat (klijentski)
            ERAC_PASS, // password za p12 certifikat

            UUID(), // messageId
            "9934:" + companyOIB, // OIB primatelja (VLASTITI) // !!! stavit pravi OIB iz baze
            FilterDateFrom, // dateFrom (dd/MM/yyyy)
            FilterDateTo  // dateTo (dd/MM/yyyy)
        );

        // MSG("ack = " + ack);
       
        // MSG("ackStatusText = " + ack["ackstatustext"]);


        if(!Contains(ack, "ErrorCode")){
            // MSG("U listi je " + Size(ack["InvoiceList"]) + " računa za preuzimanje sa FINA servera.");
            for(i = 0; i < Size(ack["InvoiceList"]); i++){
                // MSG(ack["InvoiceList"][i]);

                buyerName = ack["InvoiceList"][i]["BuyerRegistrationName"];
                buyerId = ack["InvoiceList"][i]["BuyerId"];
                buyerId = buyerId.Replace("9934:", "");

                // !!! datumi (2) ???

                supplierInvoiceId = ack["InvoiceList"][i]["SupplierInvoiceID"];
                aurasoftFormatSupplierInvoiceId = TransformInvoiceNumberToAurasoftFormat(supplierInvoiceId);

                invoiceId = ack["InvoiceList"][i]["InvoiceID"];

                statusCode = ack["InvoiceList"][i]["StatusCode"];
                if(statusCode == "RECEIVED"){
                    statusCode = "Zaprimljen";
                }elif(statusCode == "RECEIVING_CONFIRMED"){
                    statusCode = "Potvrđeno zaprimanje";
                }elif(statusCode == "APPROVED"){
                    statusCode = "Prihvaćen";
                }elif(statusCode == "REJECTED"){
                    statusCode = "Odbijen";
                }

                datumIzdavanja = ack["InvoiceList"][i]["InvoiceIssueDate"];
                // datumIzdavanja = Substring(datumIzdavanja, 0, 8);
                datumKnjizenja = ack["InvoiceList"][i]["InvoiceDate"];
                // datumKnjizenja = Substring(datumKnjizenja, 0, 8);
                // MSG(datumIzdavanja);
                // MSG(datumIzdavanja.ToString("dd/MM/yyyy") + " ... " + datumKnjizenja.ToString("dd/MM/yyyy"));

                fiskStatusAck = FINA_GET_B2B_OUTGOING_INVOICE_FISK_STATUS(
                    ERAC_SERVER + "B2BFinaInvoiceWebService/services/B2BFinaInvoiceWebService",
                    ERAC_DESC, // "e-invoice" / NE "e-racun-winx" !
                    CERTPATH + ERAC_USERT, // servisni certifikat
                    CERTPATH + ERAC_PASST, // p12 certifikat (klijentski)
                    ERAC_PASS, // password za p12 certifikat

                    UUID(), // messageId
                    "9934:" + companyOIB, // OIB primatelja (VLASTITI) // !!! stavit pravi OIB iz baze
                    invoiceId
                );

                if(Contains(fiskStatusAck, "ErrorCode")){
                    // fiskStatus = fiskStatusAck["ErrorMessage"];
                    // !!! spremit error u neki LOG
                    fiskStatus = "?";
                    reportPaymentStatus = "?";
                }else{
                    if(Contains(fiskStatusAck, "FiskStatus")){
                        fiskStatus = fiskStatusAck["FiskStatus"];
                        if(fiskStatus == "True"){
                            fiskStatus = "Da (" + fiskStatusAck["FiskStatusTimestamp"] + ")";                         
                        }elif(fiskStatus == "False"){
                            fiskStatus = "NE! (" + fiskStatusAck["FiskStatusTimestamp"] + ")";
                        }
                    }else{
                        fiskStatus = "";
                    }

                    if(Contains(fiskStatusAck, "ReportPaymentStatus")){
                        reportPaymentStatus = fiskStatusAck["ReportPaymentStatus"];
                        if(reportPaymentStatus == "True"){
                            reportPaymentStatus = "Da (" + fiskStatusAck["ReportPaymentStatusTimestamp"] + ")";
                        }elif(reportPaymentStatus == "False"){
                            reportPaymentStatus = "NE! (" + fiskStatusAck["ReportPaymentStatusTimestamp"] + ")";
                        }
                    }else{
                        reportPaymentStatus = "";
                    }
                }

                AddRacunToArrays(buyerName, buyerId, datumIzdavanja, datumKnjizenja, aurasoftFormatSupplierInvoiceId, invoiceId, statusCode, fiskStatus, reportPaymentStatus);
            }
        }else{
            MessageBox("ack[\"ErrorCode\"] = " + ack["ErrorCode"] + "\nack[\"ErrorText\"] = " + ack["ErrorText"] + "\nack[\"ErrorMessage\"] = " + ack["ErrorMessage"]);
        }
    }catch(ex){
        MessageBox("GetListFINA_B2B - Nepoznata greška:\n" + ex);
        LogException(ex); // !!!
        exit;
    }
} 

function GetListFINA_B2G(){
    // GET OUTGOING INVOICE LIST
    try{
        // MSG("Dohvaćam listu IZLAZNIH računa preko FINA-e putem WSDL-a!");
        ack = FINA_GET_B2G_OUTGOING_INVOICE_LIST(
            ERAC_SERVER + "B2GFinaInvoiceWebService/services/B2GFinaInvoiceWebService",
            ERAC_DESC, // "e-invoice" / NE "e-racun-winx" !
            CERTPATH + ERAC_USERT, // servisni certifikat
            CERTPATH + ERAC_PASST, // p12 certifikat (klijentski)
            ERAC_PASS, // password za p12 certifikat

            UUID(), // messageId
            "9934:" + companyOIB, // OIB primatelja (VLASTITI) // !!! stavit pravi OIB iz baze
            FilterDateFrom, // dateFrom (dd/MM/yyyy)
            FilterDateTo  // dateTo (dd/MM/yyyy)
        );

        // MSG("ack = " + ack);
       
        // MSG("ackStatusText = " + ack["ackstatustext"]);


        if(!Contains(ack, "ErrorCode")){
            // MSG("U listi je " + Size(ack["InvoiceList"]) + " računa za preuzimanje sa FINA servera.");
            for(i = 0; i < Size(ack["InvoiceList"]); i++){
                // MSG(ack["InvoiceList"][i]);

                buyerName = ack["InvoiceList"][i]["BuyerRegistrationName"];
                buyerId = ack["InvoiceList"][i]["BuyerId"];
                // !!! datumi (2)
                supplierInvoiceId = ack["InvoiceList"][i]["SupplierInvoiceID"];
                aurasoftFormatSupplierInvoiceId = TransformInvoiceNumberToAurasoftFormat(supplierInvoiceId);

                invoiceId = ack["InvoiceList"][i]["InvoiceID"];

                statusCode = ack["InvoiceList"][i]["StatusCode"];

                datumIzdavanja = ack["InvoiceList"][i]["InvoiceIssueDate"];
                // datumIzdavanja = Substring(datumIzdavanja, 0, 8);
                datumKnjizenja = ack["InvoiceList"][i]["InvoiceDate"];
                // datumKnjizenja = Substring(datumKnjizenja, 0, 8);


                fiskStatusAck = FINA_GET_B2G_OUTGOING_INVOICE_FISK_STATUS(
                    ERAC_SERVER + "B2GFinaInvoiceWebService/services/B2GFinaInvoiceWebService",
                    ERAC_DESC, // "e-invoice" / NE "e-racun-winx" !
                    CERTPATH + ERAC_USERT, // servisni certifikat
                    CERTPATH + ERAC_PASST, // p12 certifikat (klijentski)
                    ERAC_PASS, // password za p12 certifikat

                    UUID(), // messageId
                    "9934:" + companyOIB, // OIB primatelja (VLASTITI) // !!! stavit pravi OIB iz baze
                    invoiceId
                );

                if(Contains(fiskStatusAck, "ErrorCode")){
                    fiskStatus = fiskStatusAck["ErrorMessage"];
                    reportPaymentStatus = "";
                }else{
                    if(Contains(fiskStatusAck, "FiskStatus")){
                        fiskStatus = fiskStatusAck["FiskStatus"];
                    }else{
                        fiskStatus = "";
                    }

                    if(Contains(fiskStatusAck, "ReportPaymentStatus")){
                        reportPaymentStatus = fiskStatusAck["ReportPaymentStatus"];
                    }else{
                        reportPaymentStatus = "";
                    }
                }


                AddRacunToArrays(buyerName, buyerId, datumIzdavanja, datumKnjizenja, aurasoftFormatSupplierInvoiceId, invoiceId, statusCode, fiskStatus, reportPaymentStatus);
            }
        }else{
            MessageBox("ack[\"ErrorCode\"] = " + ack["ErrorCode"] + "\nack[\"ErrorText\"] = " + ack["ErrorText"] + "\nack[\"ErrorMessage\"] = " + ack["ErrorMessage"]);
        }
    }catch(ex){
        MessageBox("GetListFINA_B2B - Nepoznata greška:\n" + ex);
        LogException(ex); // !!!
        exit;
    }
}

// 123-90-1 -> 90100123
function TransformInvoiceNumberToAurasoftFormat(supplierInvoiceId){
    try{
        parts = supplierInvoiceId.Split("-");
        
        part0 = parts[0];
        part1 = parts[1];
        part2 = parts[2];


        parts0Length = part0.length;

        while(parts0Length < 5){
            part0 = "0" + part0;
            parts0Length = part0.length;
        }
        
        final = part1 + part2 + part0;

        return final;
    }catch(ex){
        return supplierInvoiceId;
    }
}

function GetListMER(){
    // statusId = 40; // sent(and received)
    // statusId = 30; // sent(not received)
    jsonPayload = '{"Username": "' + ERAC_USER + 
                    '", "Password": "' + ERAC_PASS + 
                    '", "CompanyId": "' + companyOIB + 
                    '", "SoftwareId": "' + ERAC_DESC + 
                    // '", "StatusId": "' + statusId + // not mandatory
                    '", "From": "' + FilterDateFrom.ToString("yyyy-MM-dd") + 
                    '", "To": "' + FilterDateTo.ToString("yyyy-MM-dd") + 'T23:59:59' +
                    '"}';
                    // MSG(jsonPayload);
            
    try {
        apiUrl = ERAC_SERVER + "apis/v2/queryoutbox";
        jsonResponse = WebRequest("POST", apiUrl, jsonPayload, "", "GetListMERSuccess", "GetListMERFailure", "application/json", null, 1000 * 20, false);
    } catch (ex) {
        MessageBox("Greška prilikom API poziva: " + ex);
        // !!! LOG !!!
        exit;
    }
}

function GetListMERSuccess(trackingId, responseCode, res) {
    // MSG("MERSuccess"); // REMOVE

    try{
        LogResponse(responseCode, res);     

        res = res.ReplaceAndTrim("[", "", "]", "");
        resParts = res.Split("},{");

        for(i = 0; i < Size(resParts); i++){
            if(i == 0){
                resParts[i] = resParts[i] + "}";
            }
            elif(i == Size(resParts) - 1){
                resParts[i] = "{" + resParts[i];
            }else{
                resParts[i] = "{" + resParts[i] + "}";
            }

            racunObject = DeserializeJson(resParts[i]);
            
            AddRacunToArrays(
                racunObject["RecipientBusinessName"],
                racunObject["RecipientBusinessNumber"],
                Substring(racunObject["Issued"], 0, 10), // datum
                "", // racunObject["Sent"], // datum
                TransformInvoiceNumberToAurasoftFormat(racunObject["DocumentNr"]),
                racunObject["ElectronicId"],
                racunObject["StatusName"] + "(" + racunObject["StatusId"] + ")",
                "", // fiskStatus
                "" // reportPaymentStatus
            );
        }
    }
    catch(ex){
        MessageBox("MERSuccess - Greška pri parsiranju odgovora poslužitelja:\n" + ex);
        // LogNKSYELOG(); // !!! LOG !!!
        exit;
    }
}

function GetListMERFailure(trackingId, responseCode, res) {
    MSG("MERFailure");

    try{
        LogResponse(responseCode, res);  
    }
    catch(ex){
        MessageBox("SendingFailure - Greška pri parsiranju odgovora poslužitelja:\n" + ex);
        // !!! LOG !!!
        exit;
    }
}


function AddRacunToArrays(supplierName, supplierId, datumIzdavanja, datumKnjizenja, supplierInvoiceId, eRacunBrojRacuna, statusRacuna, fiskStatus, reportPaymentStatus){
    arrayKupac[Rbr] = supplierName;
    arrayOibKupca[Rbr] = supplierId;
    arrayStatusRacuna[Rbr] = statusRacuna;
    arrayDatumIzdavanja[Rbr] = datumIzdavanja.ToString("dd/MM/yy");
    arrayDatumKnjizenja[Rbr] = datumKnjizenja.ToString("dd/MM/yy");
    arrayBrojRacuna[Rbr] = supplierInvoiceId;
    
    arrayIdRacunaPosrednika[Rbr] = eRacunBrojRacuna;

    arrayStatusFiskalizacije[Rbr] = fiskStatus;
    arrayReportPaymentStatus[Rbr] = reportPaymentStatus;
    
    if (PROVIDER == "MER" || PROVIDER == "MERDEMO") {
        arrayBrojRacunaMer[Rbr] = eRacunBrojRacuna;
    } elif (PROVIDER == "FINA" || PROVIDER == "FINADEMO") {
        arrayBrojRacunaFina[Rbr] = eRacunBrojRacuna;
    } else {
        MessageBox("Nepoznat provider: " + PROVIDER + ". Podržani su: MER, FINA, MERDEMO, FINADEMO");
        exit;
    }

    arrayRbr[Rbr] = ++Rbr;
}


//-----------------------------------------------------------

function btnPrikazi@clicked(){
    cntr1 = 0;
    Rbr = 0;
    cursor("wait");
    DisplayArray("datagridListaRacuna", "close");
    SetWidgetOptions("btnPrikazi", "IsEnabled", "false");
    SetWidgetOptions("StatusLabel", "Content", "Preuzimam listu izlaznih e-računa.");
    SetWidgetOptions("StatusLabel2", "Content", "Molim pričekajte...");

    AsyncCall("startGetLista", "end");   
}

function startGetLista(){
    
    getList();

}

function end(){
    cursor("dflt");
    if(Rbr != 0){
        DisplayArraySetup("datagridListaRacuna", counterFld: cntr1, activeElements: Rbr, maxElements: 10000);
    }
    SetWidgetOptions("btnPrikazi", "IsEnabled", "true");
    SetWidgetOptions("StatusLabel", "Content", "");
    SetWidgetOptions("StatusLabel2", "Content", "");
}

function LogResponse(responseCode, response) {
    if(!DIR_EXISTS(FILESPATH + "IRStatus\\" + "responses\\")){
        MAKE_DIR(FILESPATH + "IRStatus\\" + "responses\\");
    }

    logFilePath = FILESPATH + "IRStatus\\" + "responses\\" + "queryinbox" + ".response";
    appendText = Now("yyyy-MM-dd HH:mm:ss") + " -----------------------------------\n\n" + "StatusCode = " + responseCode + "\n\n" + "Response:\n" + response + "\n\n";
    try {
        SaveFile(appendText, logFilePath, "a");        
    } catch (ex) {
        MessageBox("CRITICAL: Cannot write to log file: " + logPath + "\nError: " + ex);
    }
}

function LogException(exText) {
    try{
        logDir = FILESPATH + "IRStatus\\" + "log\\";
        if(!DIR_EXISTS(logDir)){
            MAKE_DIR(logDir);
        }

        logFilePath = logDir + "wxirstatus.error.log";
        appendText = Now("yyyy-MM-dd HH:mm:ss") + " | " + exText + "\n";
        SaveFile(appendText, logFilePath, "a");
    }catch(logEx){
        MSG("LogException failed: " + logEx);
    }
}

// Export state
exportedXlsxPath = "";
exportErrorText = "";

function GetRandom8Digits(){
    raw = UUID().Replace("-", "");
    digits = "";

    for(i = 0; i < raw.length; i++){
        ch = Substring(raw, i, 1);
        if(ch >= "0" && ch <= "9"){
            digits += ch;
        }
        if(digits.length >= 8){
            break;
        }
    }

    if(digits.length < 8){
        digits += Now("HHmmssff");
    }

    return Substring(digits, 0, 8);
}

function Export2XLSX@clicked(){
    if(Rbr <= 0){
        Mbox("Nema podataka za export.");
        return;
    }

    cursor("wait");
    SetWidgetOptions("Export2XLSX", "IsEnabled", "false");
    SetWidgetOptions("StatusLabel", "Content", "Export u XLSX je u tijeku...");
    SetWidgetOptions("StatusLabel2", "Content", "Molim pričekajte...");

    AsyncCall("StartExport2XLSX", "EndExport2XLSX");
}


function StartExport2XLSX(){
    exportErrorText = "";
    exportedXlsxPath = "";

    try{
        exportFolder = FILESPATH + "IRStatus\\" + "exports\\";
        if(!Exists(exportFolder)){
            MkDir(exportFolder);
        }

        random8 = GetRandom8Digits();
        exportedXlsxPath = exportFolder + "Wxirstatus_" + random8 + ".xlsx";

        random8 = GetRandom8Digits();
        exportedXlsxPath = exportFolder + "Wxurstatus_" + random8 + ".xlsx";

        // Create and open workbook
        XFileNew(exportedXlsxPath);
        XFileOpen(exportedXlsxPath);
        XSheetRename(1, "URStatus");

        // Header row
        XCellWriteString(1, 1, 1, "Rbr");
        XCellWriteString(1, 2, 1, "Kupac");
        XCellWriteString(1, 3, 1, "OIB_kupca");
        XCellWriteString(1, 4, 1, "StatusRacuna");
        XCellWriteString(1, 5, 1, "Dat_izdav");
        XCellWriteString(1, 6, 1, "Dat_PDV");
        XCellWriteString(1, 7, 1, "Broj_racuna");
        XCellWriteString(1, 8, 1, "Fiskaliziran");
        XCellWriteString(1, 9, 1, "Status_placanja");
        XCellWriteString(1, 10, 1, "ID_racuna");

        // Header style
        for(col = 1; col <= 10; col++){
            XBackgroundColor(1, col, 1, 53, 91, 178);
            XFontColor(1, col, 1, 255, 255, 255);
            XFontFormat(1, col, 1, true, false, false, false, false, false);
        }

        // Data rows
        for(i = 0; i < Rbr; i++){
            row = i + 2;

            XCellWriteInteger(1, 1, row, arrayRbr[i]);
            XCellWriteString(1, 2, row, arrayKupac[i]);
            XCellWriteString(1, 3, row, arrayOibKupca[i]);
            XCellWriteString(1, 4, row, arrayStatusRacuna[i]);
            XCellWriteString(1, 5, row, arrayDatumIzdavanja[i]);
            XCellWriteString(1, 6, row, arrayDatumKnjizenja[i]);
            XCellWriteString(1, 7, row, arrayBrojRacuna[i]);
            XCellWriteString(1, 8, row, arrayStatusFiskalizacije[i]);
            XCellWriteString(1, 9, row, arrayReportPaymentStatus[i]);
            XCellWriteString(1, 10, row, arrayIdRacunaPosrednika[i]);
        }

        XColumnsAutoFit(1);
        XFileSave();
    }catch(ex){
        exportErrorText = ex;
        LogException("Export2XLSX error: " + ex);
        exportedXlsxPath = "";
    }
}

function EndExport2XLSX(){
    cursor("dflt");
    SetWidgetOptions("Export2XLSX", "IsEnabled", "true");
    SetWidgetOptions("StatusLabel", "Content", "");
    SetWidgetOptions("StatusLabel2", "Content", "");

    if(exportErrorText != ""){
        Mbox("Greška kod exporta u XLSX:\n" + exportErrorText);
        return;
    }

    if(exportedXlsxPath != ""){
        try{
            RunExec(exportedXlsxPath);
        }catch(ex){
            Mbox("XLSX je kreiran:\n" + exportedXlsxPath);
        }
    }
}

